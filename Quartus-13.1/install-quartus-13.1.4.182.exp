#!/usr/bin/env expect

if { $::argc != 1 } {
    puts "Error: no install path passed"
    exit 1
}

set ::env(DISPLAY) ""

set installpath [lindex $::argv 0]

set yesno "\\\[\[Yy\]/\[Nn\]\\\]"
set q2we "Quartus II Web Edition \\\(Free\\\)"

proc handle_quartus_setup {} {
    while 1 {
        expect {
            eof { break }
            -re "Press \\\[Enter\\\] to continue *:" { send "\r" }
            -re "You already had Quartus .* installed in this directory, .*, do you want to update that installation\\\? $::yesno:" {
                puts ""
                exit 1
            }
            -re "Warning: You don't have enough disk space in .* to install this " {
                expect {
                    -re "Press \\\[Enter\\\] to continue *:" {
                        puts ""
                        exit 1
                    }
                }
            }
            -re "Do you accept this license\\\? $::yesno:" { send "y\r" }
            -re "Installation \[Dd\]irectory \\\[.*\\\]:" { send "\r" }
            -re "$::q2we *$::yesno :" { send "y\r" }
            -re "$::q2we *- Quartus II Software 64-bit support \\\(.*\\\) $::yesno :" { send "y\r" }
            -re "$::q2we *- Devices $::yesno :" { send "y\r" }
            -re "$::q2we *- Devices - Arria II \\\(.*\\\) $::yesno :" { send "n\r" }
            -re "$::q2we *- Devices - Cyclone III/IV \\\(.*\\\) $::yesno :" { send "y\r" }
            -re "$::q2we *- Devices - Cyclone V \\\(.*\\\) $::yesno :" { send "y\r" }
            -re "$::q2we *- Devices - MAX II/V \\\(.*\\\) $::yesno :" { send "y\r" }
            -re "ModelSim-Altera Starter Edition \\\(Free\\\) \\\(.*\\\) $::yesno :" { send "n\r" }
            -re "ModelSim-Altera Edition \\\(.*\\\) $::yesno :" { send "n\r" }
            -re "Is the selection above correct\\\? $::yesno:" { send "y\r" }
            -re "Create shortcuts on Desktop $::yesno:" { send "n\r" }
            -re "Launch Quartus .* $::yesno:" { send "n\r" }
            -re "Provide your feedback at .* $::yesno:" { send "n\r" }
        }
    }
}

spawn ./QuartusSetupWeb-13.1.0.162.run --mode text --installdir $installpath
handle_quartus_setup

spawn ./QuartusSetup-13.1.4.182.run --mode text
while 1 {
    expect {
        eof { break }
        -re "Press \\\[Enter\\\] to continue *:" { send "\r" }
        -re "Do you accept this license\\\? $::yesno:" { send "y\r" }
        "Please choose an option \\\[1\\\] :" { send "\r" }
        # Alas! The '--installdir' option does not work.
        -re "Specify installation to update \\\[.*\\\]:" { send $installpath; send "\r" }
        "Allow patches to be uninstalled $::yesno:" { send "y\r" }
    }
}
